#!/usr/bin/awk -f

# Generate an index file from an index database. Do not forget to set the field
# separator using the -F flag. See README for more information.
# genindex -F'|' index.db > index.html

BEGIN {
	print "<ul>"
}

/^\$/ {
	print "</ul>"
	while ($0 ~ /^\$/) {
		print substr($0, 2)
		if (getline != 1) break
	}
	print "<ul>"
}

/^#|^$/ { next }

/^\+/ {
	print "</ul>"
	printf "<h3>%s</h3>\n", substr($0, 2)
	print "<ul>"
	next
}

{
	print "<li>"
	printf "<b>%s:</b>\n", $1
	printf "<a href=\"%s\">%s</a>\n", $2, $3
	if ($4) print $4
	print "</li>"
}

END {
	print "</ul>"
}
